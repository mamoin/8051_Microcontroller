ORG	00H
MOV	SP,#70H
MOV	PSW,#00H

RS	EQU P2.0
RW	EQU P2.1
ENBL	EQU P2.2

MOV 	P3,#0FFH

MOV	A,#38H
LCALL	COMMAND
LCALL	DELAY

MOV	A,#0EH
LCALL	COMMAND
LCALL	DELAY

MOV	A,#01H
LCALL	COMMAND
LCALL	DELAY


MOV	A,#06H
LCALL	COMMAND
LCALL	DELAY


MOV	A,#80H
LCALL	COMMAND
LCALL	DELAY
ACALL	DELAY

K1: 	MOV P0,#00H
	MOV A,P3
	ANL A,#00001111B
	CJNE A,#00001111B,K1

K2:	ACALL DELAY_20m
	MOV A,P3
	ANL A,#00001111B
	CJNE A,#00001111B,OVER
	SJMP K2

OVER:	ACALL DELAY_20m
	MOV A,P3
	ANL A,#00001111B
	CJNE A,#00001111B,OVER1
	SJMP K2

OVER1:	MOV P0,#11111110B
	MOV A,P3
	ANL A,#00001111B
	CJNE A,#00001111B,ROW_0
	
	MOV P0,#11111101B
	MOV A,P3
	ANL A,#00001111B
	CJNE A,#00001111B,ROW_1

	MOV P0,#11111011B
	MOV A,P3
	ANL A,#00001111B
	CJNE A,#00001111B,ROW_2

	MOV P0,#11110111B
	MOV A,P3
	ANL A,#00001111B
	CJNE A,#00001111B,ROW_3
	LJMP K2

ROW_0:  MOV DPTR,#KCODE0
	SJMP FIND

ROW_1:  MOV DPTR,#KCODE1
	SJMP FIND

ROW_2:  MOV DPTR,#KCODE2
	SJMP FIND

ROW_3:  MOV DPTR,#KCODE3
	SJMP FIND

FIND:	RRC A
	JNC MATCH
	INC DPTR
	SJMP FIND

MATCH:	CLR A
	MOVC A,@A+DPTR
	LCALL DISPLAY
	LCALL DELAY
	LJMP K1




COMMAND:LCALL READY
	MOV P1,A
	CLR RS
	CLR RW
	SETB ENBL
	LCALL DELAY
	CLR ENBL
	RET

DISPLAY:LCALL READY
	MOV P1,A
	SETB RS
	CLR RW
	SETB ENBL
	LCALL DELAY
	CLR ENBL
	RET

READY:	SETB P1.7
	CLR RS
	SETB RW

WAIT:	CLR ENBL
	ACALL DELAY
	SETB ENBL
	JB P1.7,WAIT
	RET

DELAY:	
	MOV R3,#55
	AGAIN_2:MOV R4,#255
AGAIN:  DJNZ R4,AGAIN
	DJNZ R3,AGAIN_2
	RET

DELAY_20m:	MOV	R2, #43
L3:	MOV	R3, #6
L2:	MOV	R4, #37
L1:	DJNZ	R4, L1
	DJNZ	R3, L2
	DJNZ	R2, L3
	NOP
	NOP
	RET
	
	

KCODE0: DB 'D','F','I','E'
KCODE1: DB 'C','9','8','7'
KCODE2: DB 'B','6','5','4'
KCODE3: DB 'A','3','Q','S'
	 END	
