ORG	00H
MOV	SP,#70H
MOV	PSW,#00H

RS	EQU P2.0
RW	EQU P2.1
ENBL	EQU P2.2

ORIGINAL_PASSWORD:

MOV 60H,#31H
MOV 61H,#30H
MOV 62H,#32H
MOV 63H,#34H
MOV 64H,#30H
MOV 65H,#35H

MOV 	P3,#0FFH
MOV	R0,#40H

MOV	A,#38H
LCALL	COMMAND
LCALL	DELAY

MOV	A,#0EH
LCALL	COMMAND
LCALL	DELAY

MOV	A,#01H
LCALL	COMMAND
LCALL	DELAY


MOV	A,#06H
LCALL	COMMAND
LCALL	DELAY


MOV	A,#80H
LCALL	COMMAND
LCALL	DELAY
ACALL	DELAY

	MOV DPTR,#MSG4		;PRINTING USERNAME 	

USERNAME_:
	CLR A
	MOVC A,@A+DPTR
	JZ FINISH_1
	LCALL DISPLAY
	LCALL DELAY
	INC DPTR
	LJMP USERNAME_

FINISH_1:
	MOV A,#0C0H
	LCALL COMMAND
	LCALL DELAY  

	MOV DPTR,#MSG1		;PRINTING PASSWORD 	

PASSWORD_:	
	CLR A
	MOVC A,@A+DPTR
	JZ FINISH_2
	LCALL DISPLAY
	LCALL DELAY
	INC DPTR
	LJMP PASSWORD_


FINISH_2: 
        MOV	A,#89H
	LCALL	COMMAND
	LCALL	DELAY
	
	MOV R5,#03H
	USER_: LCALL KEYPAD
	       DJNZ R5,USER_
	
	MOV	A,#0C9H
	LCALL	COMMAND
	LCALL	DELAY
	
	MOV R0,#40H
	MOV R5,#06H
	PASS_: LCALL KEYPAD
	       DJNZ R5,PASS_
 
 MOV 	R5,#06H
 MOV 	R0,#40H
 MOV 	R1,#60H

COMPARE:  MOV A,@R0
	  MOV R7,A
	  MOV A,@R1	
	  CJNE A,07H,EXIT_
	  INC R0
	  INC R1
	  DJNZ R5,COMPARE

	MOV	A,#01H 		;CLEAR SCREEN
	LCALL	COMMAND
	LCALL	DELAY
	
	MOV DPTR,#MSG2 		;ACCESS GRANTED	

GRANT_:	CLR A
	MOVC A,@A+DPTR
	JZ FINISH_7
	LCALL DISPLAY
	LCALL DELAY
	INC DPTR
	LJMP GRANT_

FINISH_7: LJMP HERE
	 
EXIT_: 	MOV	A,#01H
	LCALL	COMMAND
	LCALL	DELAY
	MOV DPTR,#MSG3 	

EXIT_1:	CLR A
	MOVC A,@A+DPTR
	JZ FINISH_8
	LCALL DISPLAY
	LCALL DELAY
	INC DPTR
	LJMP EXIT_1

FINISH_8:ACALL DELAY

HERE:	SJMP HERE
	
COMMAND:LCALL READY
	MOV P1,A
	CLR RS
	CLR RW
	SETB ENBL
	LCALL DELAY
	CLR ENBL
	RET

DISPLAY:LCALL READY
	MOV P1,A
	SETB RS
	CLR RW
	SETB ENBL
	LCALL DELAY
	CLR ENBL
	RET

READY:	SETB P1.7
	CLR RS
	SETB RW

WAIT:	CLR ENBL
	ACALL DELAY
	SETB ENBL
	JB P1.7,WAIT
	RET

DELAY:	
	MOV R3,#55
	AGAIN_2:MOV R4,#255
AGAIN:  DJNZ R4,AGAIN
	DJNZ R3,AGAIN_2
	RET

DELAY_20m:	MOV	R2, #43
L3:	MOV	R3, #6
L2:	MOV	R4, #37
L1:	DJNZ	R4, L1
	DJNZ	R3, L2
	DJNZ	R2, L3
	NOP
	NOP
	RET
KEYPAD:
K1: 	MOV P0,#00H
	MOV A,P3
	ANL A,#00001111B
	CJNE A,#00001111B,K1

K2:	ACALL DELAY_20m
	MOV A,P3
	ANL A,#00001111B
	CJNE A,#00001111B,OVER
	SJMP K2

OVER:	ACALL DELAY_20m
	MOV A,P3
	ANL A,#00001111B
	CJNE A,#00001111B,OVER1
	SJMP K2

OVER1:	MOV P0,#11111110B
	MOV A,P3
	ANL A,#00001111B
	CJNE A,#00001111B,ROW_0
	
	MOV P0,#11111101B
	MOV A,P3
	ANL A,#00001111B
	CJNE A,#00001111B,ROW_1

	MOV P0,#11111011B
	MOV A,P3
	ANL A,#00001111B
	CJNE A,#00001111B,ROW_2

	MOV P0,#11110111B
	MOV A,P3
	ANL A,#00001111B
	CJNE A,#00001111B,ROW_3
	LJMP K2

ROW_0:  MOV DPTR,#KCODE0
	SJMP FIND

ROW_1:  MOV DPTR,#KCODE1
	SJMP FIND

ROW_2:  MOV DPTR,#KCODE2
	SJMP FIND

ROW_3:  MOV DPTR,#KCODE3
	SJMP FIND

FIND:	RRC A
	JNC MATCH
	INC DPTR
	SJMP FIND

 

MATCH:	CLR A
	MOVC A,@A+DPTR
	MOV  @R0,A
	LCALL DISPLAY
	LCALL DELAY
	INC R0 
	
RET
		
	
MSG1:	DB 'PASSWORD:',0
MSG2:	DB 'ACCESS GRANTED',0
MSG3:	DB 'ACCESS DENIED',0
MSG4:	DB 'USERNAME:',0
KCODE0: DB '1','2','3','A'
KCODE1: DB '4','5','6','B'
KCODE2: DB '7','8','9','C'
KCODE3: DB '*','0','#','E'
	 END	
