ORG	00H
MOV	SP,#70H
MOV	PSW,#00H

RS	EQU P2.0
RW	EQU P2.1
ENBL	EQU P2.2

MOV 	P3,#0FFH
MOV	R0,#40H
MOV	R5,#3

MOV	A,#38H
LCALL	COMMAND
LCALL	DELAY

MOV	A,#0EH
LCALL	COMMAND
LCALL	DELAY

MOV	A,#01H
LCALL	COMMAND
LCALL	DELAY


MOV	A,#06H
LCALL	COMMAND
LCALL	DELAY


MOV	A,#80H
LCALL	COMMAND
LCALL	DELAY
ACALL	DELAY

	MOV DPTR,#MSG1 	

PASSWORD:	CLR A
	MOVC A,@A+DPTR
	JZ FINISH_1
	LCALL DISPLAY
	LCALL DELAY
	INC DPTR
	LJMP PASSWORD

FINISH_1:ACALL COMMAND
	 ACALL DELAY

K1: 	MOV P0,#00H
	MOV A,P3
	ANL A,#00001111B
	CJNE A,#00001111B,K1

K2:	ACALL DELAY_20m
	MOV A,P3
	ANL A,#00001111B
	CJNE A,#00001111B,OVER
	SJMP K2

OVER:	ACALL DELAY_20m
	MOV A,P3
	ANL A,#00001111B
	CJNE A,#00001111B,OVER1
	SJMP K2

OVER1:	MOV P0,#11111110B
	MOV A,P3
	ANL A,#00001111B
	CJNE A,#00001111B,ROW_0
	
	MOV P0,#11111101B
	MOV A,P3
	ANL A,#00001111B
	CJNE A,#00001111B,ROW_1

	MOV P0,#11111011B
	MOV A,P3
	ANL A,#00001111B
	CJNE A,#00001111B,ROW_2

	MOV P0,#11110111B
	MOV A,P3
	ANL A,#00001111B
	CJNE A,#00001111B,ROW_3
	LJMP K2

ROW_0:  MOV DPTR,#KCODE0
	SJMP FIND

ROW_1:  MOV DPTR,#KCODE1
	SJMP FIND

ROW_2:  MOV DPTR,#KCODE2
	SJMP FIND

ROW_3:  MOV DPTR,#KCODE3
	SJMP FIND

FIND:	RRC A
	JNC MATCH
	INC DPTR
	SJMP FIND

AGAIN_K1: LJMP K1

MATCH:	CLR A
	MOVC A,@A+DPTR
	MOV  @R0,A
	LCALL DISPLAY
	LCALL DELAY
	INC R0 
	DJNZ R5,AGAIN_K1
	
COMPARE_1:MOV R0,#40H
	  MOV A,@R0
	  CJNE A,#30H,EXIT_

COMPARE_2:INC R0
	  MOV A,@R0
	  CJNE A,#42H,EXIT_
	  
COMPARE_3:INC R0
	  MOV A,@R0
	  CJNE A,#43H,EXIT_

GRANT: 	MOV	A,#01H
	LCALL	COMMAND
	LCALL	DELAY
	
	MOV DPTR,#MSG2 	

GRANT_:	CLR A
	MOVC A,@A+DPTR
	JZ FINISH_2
	LCALL DISPLAY
	LCALL DELAY
	INC DPTR
	LJMP GRANT_

FINISH_2:ACALL DELAY
	SJMP HERE

EXIT_: 	MOV	A,#01H
	LCALL	COMMAND
	LCALL	DELAY
	MOV DPTR,#MSG3 	

EXIT_1:	CLR A
	MOVC A,@A+DPTR
	JZ FINISH_3
	LCALL DISPLAY
	LCALL DELAY
	INC DPTR
	LJMP EXIT_1

FINISH_3:ACALL DELAY


HERE:	SJMP HERE

COMMAND:LCALL READY
	MOV P1,A
	CLR RS
	CLR RW
	SETB ENBL
	LCALL DELAY
	CLR ENBL
	RET

DISPLAY:LCALL READY
	MOV P1,A
	SETB RS
	CLR RW
	SETB ENBL
	LCALL DELAY
	CLR ENBL
	RET

READY:	SETB P1.7
	CLR RS
	SETB RW

WAIT:	CLR ENBL
	ACALL DELAY
	SETB ENBL
	JB P1.7,WAIT
	RET

DELAY:	
	MOV R3,#55
	AGAIN_2:MOV R4,#255
AGAIN:  DJNZ R4,AGAIN
	DJNZ R3,AGAIN_2
	RET

DELAY_20m:	MOV	R2, #43
L3:	MOV	R3, #6
L2:	MOV	R4, #37
L1:	DJNZ	R4, L1
	DJNZ	R3, L2
	DJNZ	R2, L3
	NOP
	NOP
	RET
	
	
MSG1:	DB 'PASSWORD:',0
MSG2:	DB 'ACCESS GRANTED',0
MSG3:	DB 'ACCESS DENIED',0
KCODE0: DB '0','1','2','3'
KCODE1: DB '4','5','6','7'
KCODE2: DB '8','9','A','B'
KCODE3: DB 'C','D','E','F'
	 END	
